@license{
Copyright (c) 2018-2023, NWO-I CWI and Swat.engineering
All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

1. Redistributions of source code must retain the above copyright notice,
this list of conditions and the following disclaimer.

2. Redistributions in binary form must reproduce the above copyright notice,
this list of conditions and the following disclaimer in the documentation
and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
POSSIBILITY OF SUCH DAMAGE.
}
@bootstrapParser
module lang::rascal::lsp::Rename

/**
 * Rename refactoring
 *
 * Implements rename refactoring according to the LSP.
 * Renaming collects information generated by the typechecker for the module/workspace, finds all definitions and
 * uses matching the position of the cursor, and computes file changes needed to rename these to the user-input name.
 */

import Exception;
import IO;
import List;
import Location;
import Node;
import ParseTree;
import Relation;
import Set;
import String;

import lang::rascal::\syntax::Rascal;

import lang::rascalcore::check::Import;
import lang::rascalcore::check::RascalConfig;

import analysis::typepal::TypePal;

import lang::rascal::lsp::WorkspaceInfo;

import analysis::diff::edits::TextEdits;

import vis::Text;

import util::Maybe;
import util::Reflective;

alias Capture = tuple[loc def, loc use];

data IllegalRenameReason
    = invalidName(str name)
    | doubleDeclaration(loc old, set[loc] new)
    | captureChange(set[Capture] captures)
    | definitionsOutsideWorkspace(set[loc] defs)
    ;

data RenameException
    = illegalRename(loc location, set[IllegalRenameReason] reason)
    | unsupportedRename(rel[loc location, str message] issues)
    | unexpectedFailure(str message)
;

set[IllegalRenameReason] checkLegalName(str name) {
    try {
        parse(#Name, escapeName(name));
        return {};
    } catch ParseError(_): {
        return {invalidName(name)};
    }
}

set[IllegalRenameReason] checkDefinitionsOutsideWorkspace(WorkspaceInfo ws, set[loc] defs) = { definitionsOutsideWorkspace(d) | d <- groupRangeByDomain({<f, d> | d <- defs, f := d.top, f notin ws.modules}) };

set[IllegalRenameReason] checkCausesDoubleDeclarations(WorkspaceInfo ws, set[Define] currentDefs, set[Define] newDefs) {
    // Is newName already resolvable from a scope where <current-name> is currently declared?
    map[loc, Define] defMap = (def.defined: def | def <- ws.defines);
    rel[loc old, loc new] doubleDeclarations = {<cD.defined, nD.defined> | Define cD <- currentDefs
                                                                         , Define nD <- newDefs
                                                                         , isContainedIn(cD.defined, nD.scope)
                                                                         , !rascalMayOverload({cD.defined, nD.defined}, defMap)
    };

    return {doubleDeclaration(old, doubleDeclarations[old]) | old <- doubleDeclarations.old};
}

set[Define] findImplicitDefinitions(WorkspaceInfo ws, start[Module] m, set[Define] newDefs) {
    set[loc] maybeImplicitDefs = {l | /QualifiedName n := m, just(l) := locationOfName(n)};
    return {def | Define def <- newDefs, (def.idRole is variableId && def.defined in ws.useDef<0>)
                                      || (def.idRole is patternVariableId && def.defined in maybeImplicitDefs)};
}

set[IllegalRenameReason] checkCausesCaptures(WorkspaceInfo ws, start[Module] m, set[Define] currentDefs, set[loc] currentUses, set[Define] newDefs) {
    set[Define] newNameImplicitDefs = findImplicitDefinitions(ws, m, newDefs);
    set[Define] newNameExplicitDefs = newDefs - newNameImplicitDefs;

    // Will this rename turn an implicit declaration of `newName` into a use of a current declaration?
    set[Capture] implicitDeclBecomesUseOfCurrentDecl =
        {<cD, nD.defined> | Define nD <- newNameImplicitDefs
                          , <loc cS, loc cD> <- currentDefs<scope, defined>
                          , isContainedIn(nD.defined, cS)
        };

    // Will this rename hide a used definition of `oldName` behind an existing definition of `newName` (shadowing)?
    set[Capture] currentUseShadowedByRename =
        {<nD.defined, cU> | Define nD <- newDefs
                          , <cU, cS> <- ident(currentUses) o ws.useDef o ws.defines<defined, scope>
                          , isContainedIn(cU, nD.scope)
                          , isStrictlyContainedIn(nD.scope, cS)
        };

    // Will this rename hide a used definition of `newName` behind a definition of `oldName` (shadowing)?
    set[Capture] newUseShadowedByRename =
        {<cD.defined, nU> | Define nD <- newDefs
                        , nU <- invert(ws.useDef)[newDefs.defined]
                        , Define cD <- currentDefs
                        , isContainedIn(cD.scope, nD.scope)
                        , isContainedIn(nU, cD.scope)
        };

    allCaptures =
        implicitDeclBecomesUseOfCurrentDecl
      + currentUseShadowedByRename
      + newUseShadowedByRename;

    return allCaptures == {} ? {} : {captureChange(allCaptures)};
}

set[IllegalRenameReason] collectIllegalRenames(WorkspaceInfo ws, start[Module] m, set[Define] currentDefs, set[loc] currentUses, str newName) {
    set[Define] newNameDefs = {def | def:<_, newName, _, _, _, _>  <- ws.defines};

    return reasons =
        checkLegalName(newName)
      + checkDefinitionsOutsideWorkspace(ws, currentDefs.defined)
      + checkCausesDoubleDeclarations(ws, currentDefs, newNameDefs)
      + checkCausesCaptures(ws, m, currentDefs, currentUses, newNameDefs)
    ;
}

str escapeName(str name) = name in getRascalReservedIdentifiers() ? "\\<name>" : name;

// TODO Move to stdlib?
loc findSmallestContaining(set[loc] wrappers, loc l) =
    (|unknown:///| | w < it && isContainedIn(l, w) ? w : it | w <- wrappers);

// Find the smallest trees of defined non-terminal type with a source location in `useDefs`
set[loc] findNames(start[Module] m, set[loc] useDefs) {
    set[loc] names = {};
    visit(m.top) {
        case t: appl(prod(_, _, _), _): {
            if (t.src in useDefs && just(nameLoc) := locationOfName(t)) {
                names += nameLoc;
            }
        }
    }

    if (size(names) != size(useDefs)) {
        throw unsupportedRename({<l, "Cannot find the name for this definition in <m.src.top>."> | l <- useDefs - names});
    }

    return names;
}

Maybe[loc] locationOfName(Name n) = just(n.src);
Maybe[loc] locationOfName(QualifiedName qn) = just((qn.names[-1]).src);
Maybe[loc] locationOfName(FunctionDeclaration f) = just(f.signature.name.src);
Maybe[loc] locationOfName(Variable v) = just(v.name.src);
Maybe[loc] locationOfName(Declaration d) = just(d.name.src) when d is annotation
                                                              || d is \tag;
Maybe[loc] locationOfName(Declaration d) = locationOfName(d.user.name) when d is \alias
                                                                         || d is dataAbstract
                                                                         || d is \data;

default Maybe[loc] locationOfName(Tree t) = nothing();


tuple[set[IllegalRenameReason] reasons, list[TextEdit] edits] computeTextEdits(WorkspaceInfo ws, start[Module] m, set[Define] defs, set[loc] uses, str name) {
    if (reasons := collectIllegalRenames(ws, m, defs, uses, name), reasons != {}) {
        return <reasons, []>;
    }

    replaceName = escapeName(name);
    return <{}, [replace(l, replaceName) | l <- findNames(m, defs.defined + uses)]>;
}

tuple[set[IllegalRenameReason] reasons, list[TextEdit] edits] computeTextEdits(WorkspaceInfo ws, loc moduleLoc, set[Define] defs, set[loc] uses, str name) =
    computeTextEdits(ws, parseModuleWithSpaces(moduleLoc), defs, uses, name);

list[DocumentEdit] computeDocumentEdits(WorkspaceInfo ws, loc cursor, set[Define] defs, set[loc] uses, str name) {
    rel[loc file, Define defines] defsPerFile = {<d.defined.top, d> | d <- defs};
    rel[loc file, loc uses] usesPerFile = {<u.top, u> | u <- uses};

    files = defsPerFile.file + usesPerFile.file;
    map[loc, tuple[set[IllegalRenameReason] reasons, list[TextEdit] edits]] moduleResults =
        (file: <reasons, edits> | file <- files, <reasons, edits> := computeTextEdits(ws, file, defsPerFile[file], usesPerFile[file], name));

    if (reasons := union({moduleResults[file].reasons | file <- moduleResults}), reasons != {}) {
        throw illegalRename(cursor, reasons);
    }

    list[DocumentEdit] changes = [changed(file, moduleResults[file].edits) | file <- moduleResults];

    // TODO If the cursor was a module name, we need to rename files as well
    list[DocumentEdit] renames = [];

    return changes + renames;
}

list[DocumentEdit] renameRascalSymbol(Tree cursor, set[loc] workspaceFolders, PathConfig pcfg, str newName) {
    loc cursorLoc = cursor.src;

    WorkspaceInfo ws = gatherWorkspaceInfo(workspaceFolders, pcfg);

    loc useDefAtCursor = findSmallestContaining(ws.defines.defined + ws.useDef<0>, cursorLoc);
    set[Define] defs = getDefines(ws, useDefAtCursor);
    set[loc] uses = ({} | it + getUses(ws, def) | def <- defs.defined);

    return computeDocumentEdits(ws, cursorLoc, defs, uses, newName);
}

//// WORKAROUNDS

// Workaround to be able to pattern match on the emulated `src` field
data Tree (loc src = |unknown:///|(0,0,<0,0>,<0,0>));
